/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.4.1/userguide/java_library_plugin.html
 */

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()  // like Maven Central, see https://bintray.com/bintray/jcenter
}

dependencies {

    // These dependencies are used internally, but not exposed to consumers.
    implementation 'org.apache.commons:commons-text:1.2'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

    // for tools.jar
    compile files(org.gradle.internal.jvm.Jvm.current().toolsJar)

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
}


///////////////////////////////////////////////////////////////////////////
/// Maven Central upload
///

/// 1. run, using a Java 7 JDK: ./gradlew uploadArchives
/// 2. Manually release the deployment:
/// 2a.  Browse to https://oss.sonatype.org/#stagingRepositories
/// 2b.  Search for and select this repository, by group name.
/// 2c.  At the top, click "close" then "refresh" then "release"
///      (you might have to wait a little while between the clicks).


// From http://central.sonatype.org/pages/gradle.html

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
// Is this needed?
//     useGpgCmd()
    sign configurations.archives
}

group = "org.plumelib"
archivesBaseName = "options"
version = "0.2.1"

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Plume-lib Options'
        packaging 'jar'
        // optionally artifactId can be defined here
        // Is this needed?
        // artifactId options
        description 'Command-line option processing for Java.'
        url 'https://github.com/plume-lib/options'

        scm {
          connection 'scm:git:git@github.com:plume-lib/options.git'
          developerConnection 'scm:git:git@github.com:plume-lib/options.git'
          url 'git@github.com:plume-lib/options.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'mernst'
            name 'Michael Ernst'
            email 'mernst@alum.mit.edu'
          }
        }
      }
    }
  }
}
