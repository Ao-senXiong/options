// To make build/libs/options-all.jar, run: gradle shadowJar
// It must be run under JDK 7 because it includes JDK classes.
plugins { id 'com.github.johnrengelman.shadow' version '2.0.2' }

// Not "plugins { ... }" because this file is included in other build files.
apply plugin: 'java'

// Checker Framework pluggable type-checking
apply from: 'gradle/checkerframework.gradle'

// Code formatting with Google Java Format
// apply from: 'gradle/codeformatting.gradle'

dependencies {
  implementation 'org.apache.commons:commons-text:1.2'

  testImplementation 'junit:junit:4.12'

  // for tools.jar, which contains the Javadoc tool
  shadow files(org.gradle.internal.jvm.Jvm.current().toolsJar)

  // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/options/api")
  classpath = project.sourceSets.main.compileClasspath
}

// Work around https://github.com/johnrengelman/shadow/issues/324
shadowJar.doLast {
  exec {
    workingDir 'build/libs'
    commandLine 'jar', 'xf', shadowJar.archiveName, 'META-INF/MANIFEST.MF'
  }
  exec {
    workingDir 'build/libs'
    commandLine 'sed', '-i', '/^Class-Path:.*/d', 'META-INF/MANIFEST.MF'
  }
  exec {
    workingDir 'build/libs'
    commandLine 'zip', '-q', '-u', shadowJar.archiveName, 'META-INF/MANIFEST.MF'
    ignoreExitValue true  // Exit value 12 means "nothing to do"
  }
  delete {
    delete 'build/libs/META-INF'
  }
}
