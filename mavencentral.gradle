apply from: 'build.gradle'

///////////////////////////////////////////////////////////////////////////
/// Maven Central upload
///

/// 1. Increment the "version = XXX" line below.
/// 2. Run, using a Java 7 JDK: ./gradlew -b mavencentral.gradle uploadArchives
/// 3. Manually release the deployment:
/// 3a.  Browse to https://oss.sonatype.org/#stagingRepositories
/// 3b.  Search for and select this repository, by group name.
/// 3c.  At the top, click "close" then "refresh" then "release"
///      (you might have to wait a little while between the clicks).


// From http://central.sonatype.org/pages/gradle.html

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "org.plumelib"
archivesBaseName = "options"
version = "0.2.3"

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Plume-lib Options'
        packaging 'jar'
        // optionally artifactId can be defined here
        // Is this needed?
        // artifactId options
        description 'Command-line option processing for Java.'
        url 'https://github.com/plume-lib/options'

        scm {
          connection 'scm:git:git@github.com:plume-lib/options.git'
          developerConnection 'scm:git:git@github.com:plume-lib/options.git'
          url 'git@github.com:plume-lib/options.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'mernst'
            name 'Michael Ernst'
            email 'mernst@alum.mit.edu'
          }
        }
      }
    }
  }
}
